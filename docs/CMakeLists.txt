#
# Configure Man Page Installation
#
FIND_PROGRAM(GZIP_EXECUTABLE gzip)
ADD_CUSTOM_TARGET(docs ALL)
ADD_DEPENDENCIES(docs ${PROJECT_NAME})

#
# Sections and Template Strings
#
STRING(TIMESTAMP CMAKE_COMPILATION_DATE "%d/%m/%Y")
FILE(READ ${PROJECT_SOURCE_DIR}/docs/sections/directory-structure.section DOCS_FILE_DIRECTORY_LAYOUT_SECTION)
FILE(READ ${PROJECT_SOURCE_DIR}/docs/sections/reporting-bugs.section DOCS_REPORTING_BUGS_SECTION)
FILE(READ ${PROJECT_SOURCE_DIR}/docs/sections/authors.section DOCS_AUTHORS_SECTION)

#
# Configure Build and Install Target
#
FILE(MAKE_DIRECTORY ${PROJECT_BINARY_DIR}/docs)
FILE(GLOB MAN_PAGES ${PROJECT_SOURCE_DIR}/docs/*.1)
FOREACH(FILE ${MAN_PAGES})
	GET_FILENAME_COMPONENT(FILENAME ${FILE} NAME)
	CONFIGURE_FILE(
			"${FILE}"
			"${PROJECT_BINARY_DIR}/docs/${FILENAME}"
	)
	ADD_CUSTOM_COMMAND(TARGET docs
			COMMAND ${GZIP_EXECUTABLE} -c9 "${PROJECT_BINARY_DIR}/docs/${FILENAME}" > ${PROJECT_BINARY_DIR}/docs/${FILENAME}.gz
			DEPENDS docs
			COMMENT "Building manual page docs/${FILENAME}.gz")
	INSTALL(FILES ${PROJECT_BINARY_DIR}/docs/${FILENAME}.gz DESTINATION share/man/man1)
ENDFOREACH(FILE)