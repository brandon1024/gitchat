#
# Configure Unit Tests
#
FILE(MAKE_DIRECTORY ${PROJECT_BINARY_DIR}/test)
FILE(GLOB_RECURSE TEST_SRC_LIST FOLLOW_SYMLINKS ${PROJECT_SOURCE_DIR}/test/*.c ${PROJECT_SOURCE_DIR}/test/unit/*.c ${PROJECT_SOURCE_DIR}/src/*.c)
LIST(REMOVE_ITEM TEST_SRC_LIST ${PROJECT_SOURCE_DIR}/src/git-chat.c)

INCLUDE_DIRECTORIES(
		"${PROJECT_SOURCE_DIR}/include/"
		"${PROJECT_SOURCE_DIR}/test/include/"
		"${PROJECT_BINARY_DIR}/include/"
)
ADD_EXECUTABLE(${PROJECT_NAME}-unit-tests ${PROJECT_SOURCE_DIR}/test/runner.c ${PROJECT_SOURCE_DIR}/test/test-lib.c ${TEST_SRC_LIST})
ADD_TEST(NAME unit-tests COMMAND ${PROJECT_NAME}-unit-tests)

#
# Configure Integration Tests
#
FIND_PROGRAM(BASH_INTERPRETER sh)
ADD_CUSTOM_TARGET(${PROJECT_NAME}-integration-tests)
ADD_CUSTOM_COMMAND(TARGET ${PROJECT_NAME}-integration-tests COMMAND ${BASH_INTERPRETER} "${PROJECT_BINARY_DIR}/test/integration-runner.sh")

CONFIGURE_FILE(
		"${PROJECT_SOURCE_DIR}/test/integration-runner.sh"
		"${PROJECT_BINARY_DIR}/test/integration-runner.sh"
		COPYONLY
)

FILE(GLOB INTEGRATION_TESTS ${PROJECT_SOURCE_DIR}/test/integration/*.sh)
FILE(MAKE_DIRECTORY ${PROJECT_BINARY_DIR}/test)
FOREACH(INTEGRATION_TEST ${INTEGRATION_TESTS})
	GET_FILENAME_COMPONENT(FILENAME ${INTEGRATION_TEST} NAME)
	CONFIGURE_FILE(
			"${INTEGRATION_TEST}"
			"${PROJECT_BINARY_DIR}/test/integration/${FILENAME}"
			COPYONLY
	)
ENDFOREACH()

ADD_TEST(integration-tests ${BASH_INTERPRETER} "${PROJECT_BINARY_DIR}/test/integration-runner.sh")

#
# Configure Test Resources
#
FILE(GLOB_RECURSE TEST_RESOURCES RELATIVE ${PROJECT_SOURCE_DIR}/test/resources/ ${PROJECT_SOURCE_DIR}/test/resources/*)
FOREACH(RESOURCE ${TEST_RESOURCES})
	CONFIGURE_FILE(
			"${PROJECT_SOURCE_DIR}/test/resources/${RESOURCE}"
			"${PROJECT_BINARY_DIR}/test/resources/${RESOURCE}"
			COPYONLY
	)
ENDFOREACH()